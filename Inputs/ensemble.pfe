#!/bin/csh

#PBS -S /bin/csh
#PBS -N ENSEMBLE

#PBS -q long
#PBS -l walltime=72:00:00

#! PBS -q devel
#! PBS -l walltime=2:00:00

# To run on different type of CPU cores. 1200 cores together
### PBS -l select=75:ncpus=16:model=san
#PBS -l select=60:ncpus=20:model=ivy
### PBS -l select=50:ncpus=24:model=has
### PBS -l select=45:ncpus=28:model=bro
### PBS -l select=45:ncpus=28:model=bro_ele
### PBS -l select=30:ncpus=40:model=sky_ele
### PBS -l select=30:ncpus=40:model=cas_ait
### PBS -l select=9:ncpus=128:model=rom_ait

# Combine STDOUT and STDERR
#PBS -j oe

# Send email if something happens
#PBS -m abe

# Specify group (uses default group if not specified):
#PBS -W group_list=s3007

# Make sure other jobs submitted with qsub.*.pl get killed before doing much
sleep 20

setenv EVENT Event01

# cd into the directory above the run?/Event* directories
cd $PBS_O_WORKDIR

# Split the node file among the ensemble members
mkdir -p run/${EVENT}
../Scripts/SplitNodeFile.pl $PBS_NODEFILE run?/${EVENT} >& run/${EVENT}/runlog
setenv PBS_NODEFILE ./hosts

# Run the ensemble until fully done
while (! -f run/${EVENT}/LAST)
    # Driven simulation, a single run with periodically adjusted parameters:
    #../Scripts/AssimilateDst.pl -v -dF=0.1 -s=60 ${EVENT} >>& run/${EVENT}/runlog
    # Ensemble simulation with a range of parameters and restart from best member:
    ../Scripts/AssimilateDst.pl -v -s=60 ${EVENT} >>& run/${EVENT}/runlog
    foreach rundir ( run?/${EVENT} )
       (cd ${rundir}; mpiexec SWMF.exe >& runlog.`date +%y%m%d_%H%M%S`) &
    end
    wait
end

touch run/${EVENT}/DONE

echo "Finished" >> run/${EVENT}/runlog

exit
